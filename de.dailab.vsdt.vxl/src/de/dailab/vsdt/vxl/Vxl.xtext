grammar de.dailab.vsdt.vxl.Vxl with org.eclipse.xtext.common.Terminals

generate vxl "http://www.dailab.de/vsdt/vxl/Vxl"

// COMPLEX TERMS
VxlTerm:			head = VxlHead (tail = VxlTail)?;
VxlHead:			VxlBracketTerm | VxlNegation | VxlMinus | VxlAtom;
VxlTail:			operator = VxlOperator term = VxlTerm;
VxlBracketTerm:		"(" term = VxlTerm ")";
VxlNegation:		"not" head = VxlHead;
VxlMinus:			"-" head = VxlHead;
VxlAtom:			VxlValue | VxlVariable | VxlList;

// VARIABLES & ACCESSORS
VxlVariable:		name = ID (accessor = VxlAccessor)?;
VxlAccessor:		VxlArrayAccessor | VxlFieldAccessor;
VxlArrayAccessor:	"[" index = VxlTerm "]" (accessor = VxlAccessor)?;
VxlFieldAccessor:	"." name = ID (accessor = VxlAccessor)?;

// LISTS
VxlList:			"[" (body = VxlListElement)? "]"; 
VxlListElement:		first = VxlTerm ("," rest = VxlListElement)?;

// CONSTANTS
VxlValue:			VxlStringConst | VxlBooleanConst | VxlNumericConst | VxlNullConst;
VxlStringConst:		const = STRING;
//NumericConst:		const = INT; // keine kommazahlen 
VxlNumericConst:	const = NUMERIC;
// BooleanConst:	isTrue ?= "true" | "false";
VxlBooleanConst:	const = ('true' | 'false');
VxlNullConst:		const = "null";

// OPERATIONS
enum VxlOperator: 
	LT = "<" | LE = "<=" | EQ = "==" | NEQ = "!=" | GT = ">" | GE = ">=" |	// Term ** Term -> Bool
	ADD = "+" | SUB = "-" | MULT = "*" | DIV = "/" | MOD = "%" |			// Num ** Num -> Num
	AND = "and" | OR = "or" |												// Bool ** Bool -> Bool
	CONCAT = "++"; 															// String ** String -> String

// NATIVES
//terminal Numeric: "('0'..'9')+('.' ('0'..'9')+)?";
NUMERIC : INT ('.' INT)? ;